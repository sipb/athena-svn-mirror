AC_INIT(eel)

dnl ===========================================================================

ART_REQUIRED=2.3.8
GCONF_REQUIRED=1.1.11
GDK_PIXBUF_REQUIRED=2
GLIB_REQUIRED=2
GNOME_REQUIRED=2.0
GNOME_UI_REQUIRED=2.0
GNOME_VFS_REQUIRED=1.9
GTK_REQUIRED=2.1.0
POPT_REQUIRED=1.5
XML_REQUIRED=2.4.7
GAIL_REQUIRED=0.16
LIBGLADE_REQUIRED=2.0.0

AC_SUBST(ART_REQUIRED)
AC_SUBST(GCONF_REQUIRED)
AC_SUBST(GDK_PIXBUF_REQUIRED)
AC_SUBST(GLIB_REQUIRED)
AC_SUBST(GNOME_REQUIRED)
AC_SUBST(GNOME_UI_REQUIRED)
AC_SUBST(GNOME_VFS_REQUIRED)
AC_SUBST(GTK_REQUIRED)
AC_SUBST(POPT_REQUIRED)
AC_SUBST(XML_REQUIRED)
AC_SUBST(GAIL_REQUIRED)
AC_SUBST(LIBGLADE_REQUIRED)

dnl ===========================================================================

EEL_MAJOR_VERSION=2
EEL_MINOR_VERSION=2
EEL_MICRO_VERSION=4
EEL_VERSION=$EEL_MAJOR_VERSION.$EEL_MINOR_VERSION.$EEL_MICRO_VERSION
EEL_VERSION_INFO=`expr $EEL_MAJOR_VERSION + $EEL_MINOR_VERSION`:$EEL_MICRO_VERSION:$EEL_MINOR_VERSION

AC_SUBST(EEL_MAJOR_VERSION)
AC_SUBST(EEL_MINOR_VERSION)
AC_SUBST(EEL_MICRO_VERSION)
AC_SUBST(EEL_VERSION)
AC_SUBST(EEL_VERSION_INFO)

VERSION=$EEL_VERSION

AM_INIT_AUTOMAKE(eel, $VERSION)
AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_AWK
AM_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_STDC_HEADERS
AM_SANITY_CHECK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_C_CONST
AC_C_BIGENDIAN

PKG_CHECK_MODULES(EEL, \
	gconf-2.0 >= $GCONF_REQUIRED \
	gdk-pixbuf-2.0 >= $GDK_PIXBUF_REQUIRED \
	gthread-2.0 >= $GLIB_REQUIRED \
	glib-2.0 >= $GLIB_REQUIRED \
	gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED \
	gtk+-2.0 >= $GTK_REQUIRED \
	gail >= $GAIL_REQUIRED \
	libart-2.0 >= $ART_REQUIRED \
	libgnome-2.0 >= $GNOME_REQUIRED \
	libgnomeui-2.0 >= $GNOME_UI_REQUIRED \
	libxml-2.0 >= $XML_REQUIRED \
	libglade-2.0 >= $LIBGLADE_REQUIRED)
AC_SUBST(EEL_CFLAGS)
AC_SUBST(EEL_LIBS)

dnl ===========================================================================

ALL_LINGUAS="am ar az be bg bn bs ca cs da de el en_GB es et eu fa fi fr ga gl he hi hu id is it ja kn ko lt lv mk ml mn ms nl nn no pl pt pt_BR ro ru sk sl sq sr sr@Latn sv ta th tr uk vi wa zh_CN zh_TW"

GETTEXT_PACKAGE=eel-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")
AC_DEFINE_UNQUOTED(GNOME_EXPLICIT_TRANSLATION_DOMAIN, "$GETTEXT_PACKAGE")

AM_GLIB_GNU_GETTEXT

# AM_GNOME2_GETTEXT above substs $DATADIRNAME
# this is the directory where the *.{mo,gmo} files are installed
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

AC_CHECK_FUNCS(bind_textdomain_codeset)

dnl ===========================================================================

AC_PROG_AWK
AC_CHECK_PROGS(PERL, perl5 perl)
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

dnl ===========================================================================

ENABLE_PROFILER=
AC_ARG_ENABLE(profiler,
[  --enable-profiler		Enable profiler],
ENABLE_PROFILER=1
AC_DEFINE(ENABLE_PROFILER))

if test "x$ENABLE_PROFILER" = "x1"
then
	CFLAGS="-g -O -gdwarf-2 -finstrument-functions -D__NO_STRING_INLINES"
	LDFLAGS="/gnome/PROFILE/lib/libprofiler.so -lpthread"
fi

AC_SUBST(ENABLE_PROFILER)
AM_CONDITIONAL(ENABLE_PROFILER, test "x$ENABLE_PROFILER" = "x1")

dnl ===========================================================================

AC_CHECK_LIB(popt, poptParseArgvString, [POPT_LIBS="-lpopt"])
AC_SUBST(POPT_LIBS)

dnl ===========================================================================

AC_CHECK_FUNCS(setenv unsetenv putenv)

dnl ===========================================================================


#
# Checks for Xft/XRender
#
have_render=false
RENDER_LIBS=""

AC_CHECK_LIB(Xrender, XRenderFindFormat, 
    have_render=true,:,-lXext $EEL_LIBS)

if $have_render ; then
   RENDER_LIBS="-lXrender -lXext"
   AC_DEFINE(HAVE_RENDER)
fi

AC_SUBST(RENDER_LIBS)


dnl ===========================================================================

dnl Turn on the additional warnings last, so -Werror doesn't affect other tests.

AC_ARG_ENABLE(more-warnings,
[  --enable-more-warnings  Maximum compiler warnings],
set_more_warnings="$enableval",[
if test -f $srcdir/CVSVERSION; then
	is_cvs_version=true
	set_more_warnings=yes
else
	set_more_warnings=no
fi
])
AC_MSG_CHECKING(for more warnings, including -Werror)
if test "$GCC" = "yes" -a "$set_more_warnings" != "no"; then
	AC_MSG_RESULT(yes)
	CFLAGS="\
	-Wall \
	-Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes \
	-Wnested-externs -Wpointer-arith \
	-Wsign-compare \
	-Werror \
	$CFLAGS"

	for option in -Wsign-promo -Wno-sign-compare; do
		SAVE_CFLAGS="$CFLAGS"
		CFLAGS="$option $CFLAGS"
		AC_MSG_CHECKING([whether gcc understands $option])
		AC_TRY_COMPILE([], [],
			has_option=yes,
			has_option=no,)
		if test $has_option = no; then
			CFLAGS="$SAVE_CFLAGS"
		fi
		AC_MSG_RESULT($has_option)
		unset has_option
		unset SAVE_CFLAGS
	done
	unset option
else
	AC_MSG_RESULT(no)
fi

dnl ===========================================================================

AC_OUTPUT([
Makefile
eel-2.0.pc
eel.spec
eel/Makefile
eel/eel-features.h
po/Makefile.in
test/Makefile
])

dnl Initialization

AC_INIT(src/gtkhtml.c)
AM_CONFIG_HEADER(config.h)

PACKAGE=gtkhtml

GTKHTML_RELEASE=3.0
AC_DEFINE_UNQUOTED(GTKHTML_RELEASE, "${GTKHTML_RELEASE}", [GtkHTML Release])
AC_SUBST(GTKHTML_RELEASE)

GTKHTML_RELEASE_STRING=${PACKAGE}-${GTKHTML_RELEASE}
AC_DEFINE_UNQUOTED(GTKHTML_RELEASE_STRING, "${GTKHTML_RELEASE_STRING}",
					   [GtkHTML Release String])
AC_SUBST(GTKHTML_RELEASE_STRING)

dnl because intltool has an extremely broken way of getting the
dnl GETTEXT_PACKAGE name we cannot do variable substitution here
dnl so we have to hard code it instead.
dnl GETTEXT_PACKAGE=${GTKHTML_RELEASE_STRING}
GETTEXT_PACKAGE=gtkhtml-3.0
AC_SUBST(GETTEXT_PACKAGE)

GNOME_EXPLICIT_TRANSLATION_DOMAIN=${GTKHTML_RELEASE_STRING}
AC_DEFINE_UNQUOTED(GNOME_EXPLICIT_TRANSLATION_DOMAIN, "${GNOME_EXPLICIT_TRANSLATION_DOMAIN}", [Gnome Explicit Translation Domain])

AM_INIT_AUTOMAKE($PACKAGE, $GTKHTML_RELEASE.9)

EDITOR_API_VERSION=3.0
AC_DEFINE_UNQUOTED(EDITOR_API_VERSION, "${EDITOR_API_VERSION}", [Editor API Version])
AC_SUBST(EDITOR_API_VERSION)

AM_MAINTAINER_MODE

dnl
dnl Due to the sed scripts being split on 90 line
dnl blocks, this macro needs to be right at the beggining.
dnl
AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

ALL_LINGUAS="am ar az be ca cs da de el es et eu fi fr gl he hi hu it ja ko lt lv ms nl nn no pl pt pt_BR ru sk sl sr sr@Latn sv tr uk vi zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

GAIL_MODULES="gail >= 0.13"
PKG_CHECK_MODULES(GAIL, $GAIL_MODULES)
AC_SUBST(GAIL_CFLAGS)
AC_SUBST(GAIL_LIBS)
AC_SUBST(GAIL_MODULES)

GTKHTML_MODULES="libgnomeui-2.0 >= 1.112.1 libgnomeprint-2.2 >= 2.2.0 libgnomeprintui-2.2 >= 2.2.1 libglade-2.0 >= 2.0.0 gal-2.0 >= 0.0.2"
PKG_CHECK_MODULES(GTKHTML, $GTKHTML_MODULES)
AC_SUBST(GTKHTML_CFLAGS)
AC_SUBST(GTKHTML_LIBS)
AC_SUBST(GTKHTML_MODULES)

EDITOR_MODULES="${GTKHTML_MODULES} libbonoboui-2.0 >= 1.112.1"
PKG_CHECK_MODULES(EDITOR, $EDITOR_MODULES)
AC_SUBST(EDITOR_CFLAGS)
AC_SUBST(EDITOR_LIBS)

ORBIT_IDL="`$PKG_CONFIG --variable=orbit_idl ORBit-2.0`"
AC_SUBST(ORBIT_IDL)

BONOBO_IDL_INCLUDES="-I`$PKG_CONFIG --variable=idldir libbonobo-2.0` -I`$PKG_CONFIG --variable=idldir bonobo-activation-2.0`"
AC_SUBST(BONOBO_IDL_INCLUDES)

dnl **********************************
dnl soup for testgtkhtml
dnl **********************************

PKG_CHECK_MODULES(SOUP, soup-2.0 >= 0.7.9,have_soup="yes",have_soup="no")
AM_CONDITIONAL(HAVE_SOUP, test x$have_soup = xyes)
AC_SUBST(SOUP_CFLAGS)
AC_SUBST(SOUP_LIBS)

dnl **********************************
dnl Editor component
dnl **********************************

EDITOR_API_VERSION=3.0
AC_DEFINE_UNQUOTED(EDITOR_API_VERSION, "${EDITOR_API_VERSION}")
AC_SUBST(EDITOR_API_VERSION)

dnl ******************************
dnl shlib factory
dnl ******************************

AC_ARG_ENABLE(shlib-factory, [  --enable-shlib-factory  build editor factory as shared library [default=yes]],shlib_factory="$enableval", shlib_factory="yes")

if test "x$shlib_factory" = "xyes"; then
	GNOME_GTKHTML_EDITOR_SHLIB=1
	GNOME_GTKHTML_EDITOR_TYPE="shlib"
	AC_DEFINE(GNOME_GTKHTML_EDITOR_SHLIB,, [Editor shlib])
else
	GNOME_GTKHTML_EDITOR_TYPE="exe"
fi

AC_SUBST(GNOME_GTKHTML_EDITOR_TYPE)
AM_CONDITIONAL(EDITOR_SHLIB, test x$shlib_factory = xyes)

dnl **********************************
dnl Library version information.
dnl **********************************
 
dnl Increment the following if the interface has additions, changes,
dnl removals.
GTKHTML_CURRENT=3

dnl Increment any time the source changes; set to 0 if you
dnl increment CURRENT.
GTKHTML_REVISION=2

dnl Increment if any interfaces have been added; set to 0
dnl if any interfaces have been removed. removal has 
dnl precedence over adding, so set to 0 if both happened.
GTKHTML_AGE=1

AC_SUBST(GTKHTML_CURRENT)
AC_SUBST(GTKHTML_REVISION)
AC_SUBST(GTKHTML_AGE)

dnl **************
dnl Done.
dnl **************

AC_OUTPUT([
Makefile
gtkhtml.spec
po/Makefile.in
src/Makefile
src/libgtkhtml.pc
a11y/Makefile
components/Makefile
components/html-editor/Makefile
art/Makefile
])

echo "

Configuration:

	Editor component type:		${GNOME_GTKHTML_EDITOR_TYPE}
"

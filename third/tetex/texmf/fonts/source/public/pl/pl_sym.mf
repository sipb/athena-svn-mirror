% ---------------------------------------------------------------------------
% This is PL_SYM.MF ({\bif{}polskie symbole matematyczne, i.e., extra
% Polish math symbols) in text format. Characters |xleq| and |xgeq| are
% generated when their codes are known.
% The program is a modification of the SYM.MF file.
% ---------------------------------------------------------------------------
% PL VERSION 1.03,   18 Dec 1992
% ---------------------------------------------------------------------------
% AUTHORS OF MODIFICATION: {\bif{}Bogus\l{}aw Jackowski \& Marek Ry\'cko}
% ---------------------------------------------------------------------------
% This file belongs to the public domain under conditions similar to
% those of D. E. Knuth specified for the Computer Modern family of fonts.
% 
% For details see either the file MEXINFO.POL or MEXINFO.ENG.
% ---------------------------------------------------------------------------
% HISTORY
%  7 Feb 1992: version 1.00
% 11 Feb 1992: version 1.01
%    (no changes; the new version number due to changes in \MeX{} format)
%  1 Jun 1992: version 1.02
%    (no changes; the new version number due to changes in other files)
% 18 Dec 1992: version 1.03
%    (no changes; the new version number due to changes in FIK_MIK.MF
%    and some cosmetic changes in CMBX11.MF, CMR11.MF, CMTI11.MF, CMSL11.MF,
%    CMSSBI10.MF, and POLAN.MF)
% ---------------------------------------------------------------------------


% ---------------------------------------------------------------------------
iff known xleq: cmchar "Extra less than or equal to sign";
compute_spread(.45x_height#,.55x_height#);
spread#':=spread#; spread':=spread; % the spread of `$=$'
compute_spread(5/4x_height#,3/2x_height#);
beginchar(xleq,14u#,v_center(spread#'+spread#+rule_thickness#));
 italcorr h#*slant-u#;
 adjust_fit(0,0); pickup rule.nib;
 lft x2=hround 1.5u-eps; x1=x3=w-x2;
 y1-y3=spread; y2=.5[y1,y3]; top y1=h;
 draw z1--z2--z3; % diagonals
 x8=x1; x9=x2; y2-y9=y3-y8=floor(spread'); draw z8--z9;  % skew bar
 labels(1,2,3,8,9);
endchar;

iff known xgeq: cmchar "Extra greater than or equal to sign";
compute_spread(.45x_height#,.55x_height#);
spread#':=spread#; spread':=spread; % the spread of `$=$'
compute_spread(5/4x_height#,3/2x_height#);
beginchar(xgeq,14u#,v_center(spread#'+spread#+rule_thickness#));
 italcorr h#*slant-u#;
 adjust_fit(0,0); pickup rule.nib;
 lft x1=hround 1.5u-eps; x2=w-x1; x3=x1;
 y1-y3=spread; y2=.5[y1,y3]; top y1=h;
 draw z1--z2--z3;  % diagonals
 x8=x1; x9=x2; y2-y9=y3-y8=floor(spread'); draw z8--z9; % skew bar
 labels(1,2,3,8,9);
endchar;

endinput

%%\end
